#include <TinyGPS++.h>
#include <WiFi.h>
#include <HTTPClient.h>

const char* ssid = "Wokwi-GUEST";
const char* password = "";

TinyGPSPlus gps;
HardwareSerial ss(2); // Usando a serial 2 para GPS (GPIO16 e GPIO17)

void setup() {
  Serial.begin(115200);
  ss.begin(9600, SERIAL_8N1, 16, 17); // Iniciar a comunicação serial com o GPS

  // Conectar ao Wi-Fi
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao WiFi");

  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }

  Serial.println(" Conectado ao WiFi!");
}

void loop() {
  while (ss.available() > 0) {
    if (gps.encode(ss.read())) {
      if (gps.location.isValid()) {
        double latitude = gps.location.lat();
        double longitude = gps.location.lng();

        Serial.print("Latitude: ");
        Serial.println(latitude, 6);
        Serial.print("Longitude: ");
        Serial.println(longitude, 6);

        // Enviar os dados para o servidor
        enviarDados(latitude, longitude);
      }
    }
  }

  if (millis() > 5000 && gps.charsProcessed() < 10) {
    Serial.println("Nenhum dado GPS detectado. Verifique a conexão.");
    delay(5000);
  }
}

void enviarDados(double latitude, double longitude) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    String url = "http://glad-reliably-lion.ngrok-free.app/dadosGPS";
    
    http.begin(url);
    http.addHeader("Content-Type", "application/json");

    String postData = "{\"lat\":";
    postData += String(latitude, 6);
    postData += ", \"lng\":";
    postData += String(longitude, 6);
    postData += "}";

    int httpResponseCode = http.POST(postData);

    if (httpResponseCode > 0) {
      String response = http.getString();
      Serial.println("Código de resposta HTTP: " + String(httpResponseCode));
      Serial.println("Resposta do servidor: " + response);
    } else {
      Serial.print("Erro no envio da requisição HTTP. Código de erro: ");
      Serial.println(httpResponseCode);
    }

    http.end();
  } else {
    Serial.println("WiFi não conectado");
  }
}